/* -*-c-*- */

%{
/****************************************************************************
 * C# to Qt portation, Linux developing                                     *
 * flex/bison SVG parser                                                    *
 * Copyright (C) 2015-2018 by Eduard Kalinowski                             *
 * Germany, Lower Saxony, Hanover                                           *
 * eduard_kalinowski@yahoo.de                                               *
 *                                                                          *
 * C# project CNC-controller-for-mk1                                        *
 * https://github.com/selenur/CNC-controller-for-mk1                        *
 *                                                                          *
 * The Qt project                                                           *
 * https://github.com/eduard-x/cnc-qt                                       *
 *                                                                          *
 * CNC-Qt is free software; may be distributed and/or modified under the    *
 * terms of the GNU General Public License version 3 as published by the    *
 * Free Software Foundation and appearing in the file LICENSE_GPLv3         *
 * included in the packaging of this file.                                  *
 *                                                                          *
 * This program is distributed in the hope that it will be useful,          *
 * but WITHOUT ANY WARRANTY; without even the implied warranty of           *
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the            *
 * GNU General Public License for more details.                             *
 *                                                                          *
 * You should have received a copy of the GNU Lesser General Public         *
 * License along with CNC-Qt. If not, see  http://www.gnu.org/licenses      *
 ****************************************************************************/

#if defined(__clang__)
#pragma clang diagnostic push
#pragma clang diagnostic ignored "-Wdeprecated-register"
#endif

#if HAVE_CONFIG_H
# include <config.h>
#endif

#include <stdio.h>
#include <stdlib.h>
#include <string.h>
#include <ctype.h>

#ifdef __MINGW32__
#include <io.h>
#endif

#ifdef HAVE_UNISTD_H
#include <unistd.h>
#endif



#if !HAVE_STRCHR
# define strchr  index
# define strrchr rindex
#endif



// this generated header file from parse_svg.ypp
#include "parse_svg.h"


#include "Settings.h"
#include "GData.h"


extern int svg_lineno;
int svg_charno = 1;

#define YY_USER_ACTION svg_charno += svg_leng; 


%}


WSPACE   [ \t\r\f]+
DIGITS   [0-9]+
INTEGER  [+-]?{DIGITS}
REAL1    [+-]?{DIGITS}?[.]{DIGITS}
REAL2    [+-]?{DIGITS}([.]{DIGITS})?
REAL3    {REAL2}[eE][+-]?{DIGITS}
HEXNUM   #[0-9A-F]+
IDNAME   [A-Z\-]+{DIGITS}?
ID       {IDNAME}(":"{IDNAME})?
URL      "\""["HTTP:\/"|"HTTPS:\/"].*[^\"]
PARAM    [A-Z]
EOL      [\n]+
VERS     [0-9]+\.[0-9]+(\.[0-9]+)?
REL      {WSPACE}?"r"[0-9]*
BEG      {WSPACE}?"<"
END      ">"{WSPACE}?
ANY_TEXT [0-9A-Z\s\;_\:]{2,}
OPER     [\(\)]


%x TAG ATTRIB DATAFIELD

%option stack caseless yylineno noyywrap nounput noinput prefix="svg_"

%%

<INITIAL,TAG,ATTRIB>{EOL} {
    svg_charno = 1;
    return NEW_LINE;
}

<INITIAL,TAG,ATTRIB>{WSPACE} {
    /* skip */
}

<INITIAL>{BEG}\?.*\?{END} { 
    /* fisrt xml line 
     * TODO: encoding */ 
} 

<INITIAL>{BEG}"!--".*"--"{END} {
    /* ignore comments */
} 

<INITIAL>{BEG} {
    yy_push_state(TAG);
}

<TAG>\/{ID}{END} {  /* end of tag */
    yy_pop_state();
}

<TAG>"/"{END} { /* end of tag */
    yy_pop_state();
}

<TAG>{ID} {
    svg_lval.s_val = strdup(svg_text);
    yy_push_state(ATTRIB);
    return NAME;
}

<ATTRIB>{END} {
    yy_pop_state();
}

<ATTRIB>{ID} {
    svg_lval.s_val = strdup(svg_text);
    return NAME;
}

<ATTRIB>"=" {
    /* ignore this part */
}

<ATTRIB>"\"" { 
    /* begin of datafield */
    yy_push_state(DATAFIELD);
}

<DATAFIELD>"\"" {
    /* end of datafield */
    yy_pop_state();
}

<DATAFIELD>{WSPACE}+ {
    return ' ';
}

<DATAFIELD>{EOL} {
    svg_charno = 1;
    /* ignore */
}

<DATAFIELD,ATTRIB>{URL} { 
    /* ignore this part */
    /*return NEW_LINE;*/
}

<DATAFIELD>{HEXNUM} {
    svg_lval.i_val = strtol(&svg_text[1], NULL, 16);
    return HEXVAL;
}

<DATAFIELD>{IDNAME}":" {
    svg_lval.s_val = strndup(svg_text, strlen(svg_text)-1);
    return STR;
}

<DATAFIELD>{OPER} {
    /* svg_lval.c_val = svg_text[0]; 
    return OPER; */
}

<DATAFIELD>{INTEGER} { 
    svg_lval.f_val = (float)strtol(svg_text, NULL, 10); 
    return FLOAT; 
}

<DATAFIELD>{PARAM}{WSPACE}? { 
    switch (svg_text[0]) {
        case 'M': // moveto x y
            return MOVE_ABS;
        case 'm': // moveto dx dy
            return MOVE_REL;
        case 'L': // lineto 
            return LINETO_ABS;
        case 'l': // lineto dx dy
            return LINETO_REL;
        case 'H': // horizontal lineto
            return HLINE_ABS;
        case 'h': // horizontal lineto dx
            return HLINE_REL;
        case 'V': // vertical lineto
            return VLINE_ABS;
        case 'v': // vertical lineto dy
            return VLINE_REL;
        case 'C': // curveto
            return CURVE_ABS;
        case 'c': // curveto dx dy
            return CURVE_REL;
        case 'S': // smooth curveto 
            return SMOOTH_ABS;
        case 's': // smooth curveto 
            return SMOOTH_REL;
        case 'Q': // quadratic Bézier curve 
            return QCURVE_ABS;
        case 'q': // quadratic Bézier curve 
            return QCURVE_REL;
        case 'T': // smooth quadratic Bézier curveto 
            return SMOOTH_QCURVE_ABS;
        case 't': // smooth quadratic Bézier curveto 
            return SMOOTH_QCURVE_REL;
        case 'A': // elliptical Arc 
            return ARC_ABS;
        case 'a': // elliptical Arc 
            return ARC_REL;
        case 'Z': // closepath
        case 'z': 
            return CLOSEPATH;
        default:
            fprintf (stderr, "line %d: syntax error, unrecognized character: `%s'\n", svg_lineno, svg_text);
            return InvalidCharacter;
            break;
    }
    
    /*return CHAR; */
}

<DATAFIELD>{VERS}{WSPACE}{REL} {
    svg_lval.s_val = strdup(svg_text);
    return STR;
}

<DATAFIELD>"," { 
    /* separator for real numbers pairs */
    return ' ';
}

<DATAFIELD>{REAL1}|{REAL2}|{REAL3} {
    std::string s = svg_text;
    std::replace( s.begin(), s.end(), Settings::toDecimalPoint, Settings::fromDecimalPoint);
    svg_lval.f_val = strtof(s.c_str(), NULL);
    return FLOAT;
}

<DATAFIELD>{ANY_TEXT} {
    svg_lval.s_val = strdup(svg_text);
    return STR;
}

. { /* any other character is invalid */
  fprintf (stderr, "line %d: position %d syntax error, unrecognized character: `%s'\n", svg_lineno, svg_charno, svg_text);
   return InvalidCharacter;
}

%%

                  
